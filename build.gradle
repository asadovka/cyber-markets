/* Gradle plugins part */
buildscript {
    ext {
        kotlinVersion = "1.2.31"
        kotlinCoroutinesVersion = "0.19.3"
        jacksonVersion = "2.9.2"
        httpClientVersion = "4.5.5"
        kafkaVersion = "1.0.0"
        web3jVersion = "2.3.1"
        rxkotlinVersion = "2.2.0"
        reactorVersion = "3.1.3.RELEASE"
        xchangeStreamVersion = "4.3.2"

        springBootVersion = "2.0.0.RELEASE"
        springKafkaVersion = "2.1.2.RELEASE"
        springCassandraVersion = "2.0.3.RELEASE"

        // plugins
        dependencyPluginVersion = "1.0.3.RELEASE"
        shadowPluginVersion = "2.0.1"

        // tests
        junitVersion = "5.0.3"
        junitPlatformVersion = "1.0.3"
        mockitoVersion = "2.1.0"
        mockitoKotlinVersion = "0.7.0"

        // logs
        slf4jVersion = "1.7.25"
        lockbackVersion = "1.2.3"

        cassandraConfluentVersion = "3.3.2"
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev/' }
        maven { url 'https://repo.spring.io/libs-snapshot' }
        maven { url 'https://repo.spring.io/libs-milestone' }
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion")
        classpath("org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion")

        classpath("io.spring.gradle:dependency-management-plugin:$dependencyPluginVersion")
        classpath("org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion")
        classpath("com.github.jengelman.gradle.plugins:shadow:$shadowPluginVersion")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
    distributionType = Wrapper.DistributionType.ALL
}


subprojects {
    apply plugin: "kotlin"
    apply plugin: 'kotlin-spring'
    apply plugin: "org.junit.platform.gradle.plugin"
    apply plugin: "io.spring.dependency-management"

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    kotlin {
        experimental {
            coroutines "enable"
        }
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://repo.spring.io/libs-milestone' }
        maven { url 'https://repo.spring.io/libs-snapshot' }
    }

    dependencyManagement {
        dependencies {
            imports {
                mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
            }

            dependency("org.slf4j:slf4j-api:$slf4jVersion")
            dependency("ch.qos.logback:logback-classic:$lockbackVersion")

            dependency("org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion")
            dependency("org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion")
            dependency("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")
            dependency("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinCoroutinesVersion")

            dependency("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
            dependency("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion") {
                exclude 'org.jetbrains.kotlin:kotlin-stdlib'
            }

            dependency("org.springframework.kafka:spring-kafka:$springKafkaVersion")

            dependency("org.springframework.boot:spring-boot-starter:$springBootVersion")
            dependency("org.springframework:spring-boot-starter-test:$springBootVersion")
            dependency("org.springframework.boot:spring-boot-starter-webflux:$springBootVersion")
            dependency("org.springframework.boot:spring-boot-starter-reactor-netty:$springBootVersion")

            dependency("org.apache.kafka:kafka-clients:$kafkaVersion")

            dependency("org.junit.jupiter:junit-jupiter-api:$junitVersion")
            dependency("org.junit.platform:junit-platform-launcher:$junitPlatformVersion")
            dependency("org.mockito:mockito-core:$mockitoVersion")
            dependency("com.nhaarman:mockito-kotlin:$mockitoKotlinVersion") {
                exclude 'org.jetbrains.kotlin:kotlin-stdlib'
            }
            dependency("org.junit.jupiter:junit-jupiter-engine:$junitVersion")

            //dependency("org.springframework.data:spring-data-cassandra:$springCassandraVersion")
            //dependency("org.springframework.data:spring-data-commons:$springCassandraVersion")
            dependency("com.datastax.cassandra:cassandra-driver-core:$cassandraConfluentVersion")
            dependency("com.datastax.cassandra:cassandra-driver-mapping:$cassandraConfluentVersion")

            dependency("io.reactivex.rxjava2:rxkotlin:$rxkotlinVersion") {
                exclude 'org.jetbrains.kotlin:kotlin-stdlib'
            }
            dependency("io.projectreactor:reactor-core:$reactorVersion")

            dependency("info.bitrich.xchange-stream:xchange-stream-core:$xchangeStreamVersion")
            dependency("info.bitrich.xchange-stream:xchange-binance:$xchangeStreamVersion")
            dependency("info.bitrich.xchange-stream:xchange-bitfinex:$xchangeStreamVersion")
            dependency("info.bitrich.xchange-stream:xchange-bitstamp:$xchangeStreamVersion")
            dependency("info.bitrich.xchange-stream:xchange-gdax:$xchangeStreamVersion")
            dependency("info.bitrich.xchange-stream:xchange-poloniex2:$xchangeStreamVersion")
        }
    }

    dependencies {
        testCompile("org.junit.jupiter:junit-jupiter-api")
        testCompile("org.junit.platform:junit-platform-launcher")
        testCompile("org.mockito:mockito-core")
        testCompile("com.nhaarman:mockito-kotlin")
        testRuntime("org.junit.jupiter:junit-jupiter-engine")
    }
}

project(":core") {

    dependencies {
        compile("org.slf4j:slf4j-api")
        compile("ch.qos.logback:logback-classic")

        compile("org.jetbrains.kotlin:kotlin-stdlib-jre8")
        compile("org.jetbrains.kotlin:kotlin-reflect")
        compile("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8")

        compile("com.fasterxml.jackson.core:jackson-databind")
        compile("com.fasterxml.jackson.module:jackson-module-kotlin")

        compile("org.apache.kafka:kafka-clients")

        compile("io.reactivex.rxjava2:rxkotlin")
    }
}

project(":stream-api") {
    apply plugin: "application"

    dependencies {
        compile(project(":core"))
        compile("io.undertow:undertow-core")
    }

    mainClassName = "fund.cyber.markets.StreamApiApplication"
}

project(":rest-api") {

    apply plugin: "org.springframework.boot"

    dependencies {
        compile project(":core")
        compile project(":cassandra-service")

        compile 'org.springframework.boot:spring-boot-starter'
        compile 'org.springframework.boot:spring-boot-starter-webflux'
        compile 'org.springframework.boot:spring-boot-starter-reactor-netty'
    }
}

project(":exchanges-connector") {

    apply plugin: "org.springframework.boot"

    dependencies {
        compile project(":core")

        compile("org.springframework.boot:spring-boot-starter")
        compile("org.springframework.kafka:spring-kafka")


        //missing hitbtc connector
        compile("info.bitrich.xchange-stream:xchange-stream-core")
        compile("info.bitrich.xchange-stream:xchange-binance")
        compile("info.bitrich.xchange-stream:xchange-bitfinex")
        compile("info.bitrich.xchange-stream:xchange-bitstamp")
        compile("info.bitrich.xchange-stream:xchange-gdax")
        compile("info.bitrich.xchange-stream:xchange-poloniex2")
    }
}

project(":tickers") {

    apply plugin: "org.springframework.boot"

    springBoot {
        mainClassName = 'fund.cyber.markets.TickersApplication'
    }

    dependencies {
        compile project(":core")
        compile project(":cassandra-service")

        compile("org.springframework.boot:spring-boot-starter")
        compile("org.springframework.kafka:spring-kafka")

        testCompile("org.springframework.boot:spring-boot-starter-test")
    }
}

project(":cassandra-service") {

    apply plugin: "kotlin-noarg"

    noArg {
        annotation("com.datastax.driver.mapping.annotations.Table")
        annotation("com.datastax.driver.mapping.annotations.UDT")
    }

    dependencies {
        compileOnly project(":core")

        compileOnly("org.springframework.boot:spring-boot-starter")

        compile("com.datastax.cassandra:cassandra-driver-core")
        compile("com.datastax.cassandra:cassandra-driver-mapping")
    }
}